Sub 출납기록()

    Dim r입력위치 As Range
    Dim d입력일 As Date
    Dim i개수 As Integer
    Dim i행번호 As Integer
    Dim i표시작행 As Integer
    
    i표시작행 = Range("금전출납부").Cells(1).Row
    d입력일 = Range("A5").Value
    i개수 = WorksheetFunction.CountIf(Range("금전출납부[일자]"), ">" & d입력일)
    
    If i개수 > 0 Then
    
        i행번호 = Range("금전출납부[일자]").Rows.Count - i개수 + i표시작행
        Cells(i행번호, "A").Resize(1, 6).Insert
        
        Set r입력위치 = Cells(i행번호, "A")
        
    Else
    
        Set r입력위치 = Cells(Rows.Count, "A").End(xlUp)
        
        If Sheet1.ListObjects("금전출납부").ListRows.Count > 0 Then
            Set r입력위치 = r입력위치.Offset(1)
        End If
    
    End If
    
    With r입력위치
        If Range("A5").Value <> "" Then .Offset(, 0).Value = Range("A5").Value
        If Range("B5").Value <> "" Then .Offset(, 1).Value = Range("B5").Value
        If Range("C5").Value <> "" Then .Offset(, 2).Value = Range("C5").Value
        If Range("D5").Value <> "" Then .Offset(, 3).Value = Range("D5").Value
        If Range("E5").Value <> "" Then .Offset(, 5).Value = Range("E5").Value
    End With
    
    If MsgBox("같은 날짜의 다른 항목을 입력하시겠습니까?", vbYesNo, "출납내역") = vbYes Then
        Range("B5:E5").ClearContents
    Else
        Range("A5:E5").ClearContents
    End If

End Sub