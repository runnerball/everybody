Dim i주5일 As Integer, i주6일 As Integer, i격주 As Integer, i홀수주 As Integer

Sub 초과근무시간계산()

    Dim sMsg As String
    Dim r근태내역 As Range, r셀 As Range
    Dim i선택년 As Integer, i선택월 As Integer
    Dim d정규출근시간 As Date, d정규퇴근시간 As Date, d야간근무시간 As Date
    Dim i30분 As Integer
    Dim d실제출근시간 As Date, d실제퇴근시간 As Date, d출근일 As Date
    Dim si근무시간 As Single

    If 옵션표확인 = False Then
        sMsg = "옵션표의 근무형태와 주말 선택 옵션이 맞지 않으니 확인해 주세요!"
        MsgBox sMsg, vbCritical + vbOKOnly, "옵션표"
        Exit Sub
    End If

    Set r근태내역 = Worksheets("근태기록").UsedRange
    
    i선택년 = Range("연도").Value
    i선택월 = Range("월").Value
    d정규출근시간 = Range("출근시간").Value
    d정규퇴근시간 = Range("퇴근시간").Value
    d야간근무시간 = Range("야간근무시간").Value
            
    With ActiveSheet
        i30분 = .Shapes("opt30분").ControlFormat.Value
        i주5일 = .Shapes("opt주5일").ControlFormat.Value
        i주6일 = .Shapes("opt주6일").ControlFormat.Value
        i격주 = .Shapes("opt격주").ControlFormat.Value
        i홀수주 = IIf(.Shapes("chk홀수주휴무").ControlFormat.Value = xlOn, 1, 0)
    End With
    
    Worksheets("근태기록").Columns("E:G").ClearContents
    
    For Each r셀 In r근태내역.Columns(3).Cells
    
        If IsDate(r셀.Value) Then
        
            d실제출근시간 = r셀.Value
            d실제퇴근시간 = r셀.Offset(, 1).Value
            d출근일 = Int(d실제출근시간)
                    
            If Format(d실제출근시간, "yyyym") = i선택년 & i선택월 Then
                
                If 휴일여부(d출근일) = True Or 주말여부(d출근일) = True Then
                
                    si근무시간 = (d실제퇴근시간 - d실제출근시간) * 24
                    r셀.Offset(, 4).Value = 근무시간인정(si근무시간, i30분)
                
                Else
                    
                    If d실제퇴근시간 > (d출근일 + d야간근무시간) Then
                        
                        si근무시간 = (d야간근무시간 - d정규퇴근시간) * 24
                        r셀.Offset(, 2).Value = 근무시간인정(si근무시간, i30분)
                        
                        si근무시간 = (d실제퇴근시간 - (d출근일 + d야간근무시간)) * 24
                        r셀.Offset(, 3).Value = 근무시간인정(si근무시간, i30분)
                        
                    Else
                    
                        If d실제퇴근시간 > (d출근일 + d정규퇴근시간) Then
                            
                            si근무시간 = (d실제퇴근시간 - (d출근일 + d정규퇴근시간)) * 24
                            r셀.Offset(, 2).Value = 근무시간인정(si근무시간, i30분)
                        
                        End If
                        
                    End If
                    
                End If
    
            End If
    
        End If
        
    Next
    
    Call 계산결과요약
        
End Sub