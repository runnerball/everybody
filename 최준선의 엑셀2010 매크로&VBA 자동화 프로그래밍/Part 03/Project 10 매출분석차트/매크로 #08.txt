Sub 차트원본테이블구성()

    Dim d시작일 As Date
    Dim d종료일 As Date
    Dim r피벗 As Range
    
    On Error Resume Next
    
        Select Case ActiveSheet.Shapes(Application.Caller).TextFrame.Characters.Text
        
            Case "최근 일주일"
                d시작일 = Date - 7
                d종료일 = Date - 1
            Case "보름"
                d시작일 = Date - 14
                d종료일 = Date - 1
            Case "한달"
                d시작일 = DateAdd("m", -1, Date)
                d종료일 = Date - 1
            Case "사용자 지정"
                d시작일 = Range("사용자지정_시작일").Value
                d종료일 = Range("사용자지정_종료일").Value
        End Select
            
        If Err.Number <> 0 Then
            d시작일 = Range("사용자지정_시작일").Value
            d종료일 = Range("사용자지정_종료일").Value
        End If
        
    On Error GoTo 0
            
    Application.EnableEvents = False
        Range("사용자지정_시작일").Value = d시작일
        Range("사용자지정_종료일").Value = d종료일
    Application.EnableEvents = True
    
    Range("시작일").Value = ">=" & d시작일
    Range("종료일").Value = "<=" & d종료일
    
    Application.ScreenUpdating = False
    
        With Worksheets("집계")
        
            .Visible = xlSheetVisible
            .Activate
            
            .PivotTables(1).PivotCache.Refresh
            
            Set r피벗 = .PivotTables(1).TableRange1
            With r피벗
                Set r피벗 = .Resize(.Rows.Count - 1, .Columns.Count)
            End With
            
            r피벗.AdvancedFilter Action:=xlFilterCopy, CriteriaRange:=Range("E1:F2"), CopyToRange:=Range("E4:G4"), Unique:=False
            
            .Visible = xlSheetHidden
        
        End With
    
        Worksheets("매출분석").Activate
    
    Application.ScreenUpdating = True
    
    Call 차트생성
    
End Sub